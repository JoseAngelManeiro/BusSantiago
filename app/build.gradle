apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

// First, we try to access to CircleCI environment variable,
// if this is a local build, we obtain api key from a local file
def getApiKeyDebug() {
    def apiKeyDebug = System.getenv("API_KEY_DEBUG")
    if (apiKeyDebug == null || apiKeyDebug.length() == 0) {
        def apiKeyPropertiesFile = rootProject.file("apikey.properties")
        def apiKeyProperties = new Properties()
        apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
        apiKeyDebug = apiKeyProperties['apikeyDebug']
    }
    return apiKeyDebug
}

def getApiKeyRelease() {
    def apiKeyRelease = System.getenv("API_KEY_RELEASE")
    if (apiKeyRelease == null || apiKeyRelease.length() == 0) {
        def apiKeyPropertiesFile = rootProject.file("apikey.properties")
        def apiKeyProperties = new Properties()
        apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
        apiKeyRelease = apiKeyProperties['apikeyRelease']
    }
    return apiKeyRelease
}

android {
    defaultConfig {
        applicationId "org.galio.bussantiago"
        namespace "org.galio.bussantiago"
        compileSdk 34
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 53
        versionName "4.2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "app_version", "v${defaultConfig.versionName}"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            resValue("string", "google_maps_api_key", getApiKeyDebug())
        }
        release {
            resValue("string", "google_maps_api_key", getApiKeyRelease())

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        lintConfig file("$rootDir/app/lint-rules.xml")
        abortOnError true
        ignoreWarnings false
        htmlReport true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = '21'
    }

    buildFeatures {
        compose = true
        viewBinding = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.7"
    }
}

dependencies {
    implementation project(':widget')
    implementation project(':core')
    implementation project(':shared')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.12.0'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Navigation
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'

    // Koin
    implementation 'io.insert-koin:koin-core:3.0.1'
    implementation 'io.insert-koin:koin-android:3.0.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.10.1'

    // Maps & Location
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    // Play Core (In-app reviews)
    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'

    // Compose
    implementation "androidx.compose.runtime:runtime:1.1.1"
    implementation "androidx.compose.ui:ui:1.1.1"
    implementation "androidx.compose.foundation:foundation:1.1.1"
    implementation "androidx.compose.foundation:foundation-layout:1.1.1"
    implementation "androidx.compose.material:material:1.1.1"
    implementation "androidx.compose.runtime:runtime-livedata:1.1.1"
    implementation "androidx.compose.ui:ui-tooling:1.1.1"
    implementation "com.google.android.material:compose-theme-adapter:1.1.1"

    // Coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0")

    // Core Library Desugaring
    implementation "com.android.tools:desugar_jdk_libs:2.0.4"

    // Mockito
    testImplementation 'org.mockito:mockito-core:4.5.1'
    // Mockito (final classes)
    testImplementation 'org.mockito:mockito-inline:4.5.1'

    testImplementation 'junit:junit:4.13'

    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    // For GrantPermissionRule
    androidTestImplementation "androidx.test:rules:1.3.0"
}
