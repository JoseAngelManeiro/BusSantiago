apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'

// First, we try to access to CircleCI environment variable,
// if this is a local build, we obtain api key from a local file
def getApiKeyDebug() {
    def apiKeyDebug = System.getenv("API_KEY_DEBUG")
    if (apiKeyDebug == null || apiKeyDebug.length() == 0) {
        def apiKeyPropertiesFile = rootProject.file("apikey.properties")
        def apiKeyProperties = new Properties()
        apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
        apiKeyDebug = apiKeyProperties['apikeyDebug']
    }
    return apiKeyDebug
}

def getApiKeyRelease() {
    def apiKeyRelease = System.getenv("API_KEY_RELEASE")
    if (apiKeyRelease == null || apiKeyRelease.length() == 0) {
        def apiKeyPropertiesFile = rootProject.file("apikey.properties")
        def apiKeyProperties = new Properties()
        apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))
        apiKeyRelease = apiKeyProperties['apikeyRelease']
    }
    return apiKeyRelease
}

// Load keystore properties file for local builds
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    defaultConfig {
        applicationId "org.galio.bussantiago"
        namespace "org.galio.bussantiago"
        compileSdk rootProject.ext.compileSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 57
        versionName "4.2.7"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "app_version", "v${defaultConfig.versionName}"
    }

    signingConfigs {
        release {
            // Check if keystore.properties exists and use its values, otherwise fallback to CI environment variables
            storeFile file(keystorePropertiesFile.exists() ? keystoreProperties['storeFile'] : (System.getenv("KEYSTORE") ?: "keystore.jks"))
            storePassword keystorePropertiesFile.exists() ? keystoreProperties['storePassword'] : System.getenv("STORE_PASSWORD")
            keyAlias keystorePropertiesFile.exists() ? keystoreProperties['keyAlias'] : System.getenv("KEY_ALIAS")
            keyPassword keystorePropertiesFile.exists() ? keystoreProperties['keyPassword'] : System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            resValue("string", "google_maps_api_key", getApiKeyDebug())
        }
        release {
            resValue("string", "google_maps_api_key", getApiKeyRelease())

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    lint {
        abortOnError true
        checkAllWarnings true
        ignoreWarnings false
        htmlReport true
        checkDependencies true
        // Disable the check for invalid references like java.adapter due to
        // "Invalid package reference in com.android.tools:desugar_jdk_libs_configuration" error
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = '21'
    }

    buildFeatures {
        compose = true
        viewBinding = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
}

dependencies {
    implementation project(':widget')
    implementation project(':core')
    implementation project(':shared')

    implementation(libs.appcompat)
    implementation(libs.constraintlayout)
    implementation(libs.material)

    // ViewModel
    implementation(libs.viewmodel)

    // Navigation
    implementation(libs.navigation.ui)
    implementation(libs.navigation.fragment)

    // Koin
    implementation(libs.koin)

    // Maps & Location
    implementation(libs.play.services.maps)
    implementation(libs.play.services.location)

    // Play Core (In-app reviews)
    implementation(libs.play.review.core)
    implementation(libs.play.review.ktx)

    // Compose
    implementation(libs.compose.ui)
    implementation(libs.compose.foundation)
    implementation(libs.compose.material)
    implementation(libs.compose.runtime.livedata)
    implementation(libs.compose.ui.tooling.preview)
    debugImplementation(libs.compose.ui.tooling)
    implementation(libs.compose.theme.adapter)

    // Coroutines
    implementation(libs.coroutines)

    // Mockito
    testImplementation(libs.mockito)

    // Provides a mock Android environment for the Navigator
    testImplementation(libs.roboelectric)

    testImplementation(libs.junit)

    testImplementation(libs.arch.core.testing)

    androidTestImplementation(libs.test.runner)
    androidTestImplementation(libs.junit.ext)
    androidTestImplementation(libs.espresso)
    // For GrantPermissionRule
    androidTestImplementation(libs.test.rules)
}
