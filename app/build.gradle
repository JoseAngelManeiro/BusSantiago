apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Load keystore properties file
def keystorePropertiesFile = rootProject.file("keystore.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Load apikey properties file
def apikeyPropertiesFile = rootProject.file("apikey.properties");
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "org.galio.bussantiago"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 40
        versionName "4.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "app_version", "v${defaultConfig.versionName}"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            resValue("string", "google_maps_api_key", apikeyProperties['apikeyDebug'])
        }
        release {
            resValue("string", "google_maps_api_key", apikeyProperties['apikeyRelease'])

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    ['src/main/res/layouts/app',
                     'src/main/res/layouts/widget',
                     'src/main/res/layouts',
                     'src/main/res']
        }
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        lintConfig file("$rootDir/app/lint-rules.xml")
        abortOnError true
        ignoreWarnings false
        htmlReport true
        htmlOutput file("$rootDir/app/lint-results.html")
    }
}

dependencies {
    implementation project(':core')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'

    // Koin
    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Maps & Location
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Mockito
    testImplementation 'org.mockito:mockito-core:2.23.4'
    // Mockito (final classes)
    testImplementation 'org.mockito:mockito-inline:2.23.4'

    testImplementation 'junit:junit:4.13'

    testImplementation 'androidx.arch.core:core-testing:2.1.0'
}
